loadweaplist = ""
loadweapsave = 0
loadweapall = 1
setpersist loadweapall 1
setcomplete loadweapall 1

loadweaps = [
    lwa = ""
    lwo = (? (> (listlen $loadweaplist) $arg1) (at $loadweaplist $arg1) 0)
    loop w2 (min (listlen $loadweaplist) $weapidxloadout) [
        if (= $w2 $arg1) [
            lwa = (? $w2 (concat $lwa $arg2) $arg2)
        ] [
            w3 = (? (> (listlen $loadweaplist) $w2) (at $loadweaplist $w2) 0)
            if (&& $w3 (= $arg2 $w3)) [ w3 = $lwo ]
            lwa = (? $w2 (concat $lwa $w3) $w3)
        ]
    ]
    if (listlen $lwa) [ loadweaplist = $lwa ]
]

newgui loadout [
    guione [
        guilist [
            guilist [
                guibackground
                guicenter [
                    guistayopen [
                        cnt = (? $loadweapall $weapidxloadout $maxcarry)
                        gdw = 0
                        loop w2 $cnt [
                            w3 = (? (> (listlen $loadweaplist) $w2) (at $loadweaplist $w2) 0)
                            hi = (mod $w2 2)
                            al = (|| (= $w3 0) (allowedweap $w3))
                            guilist [
                                guibackground (? (&& $al (< $gdw $maxcarry)) 0x303030 0x000000)
                                guistrut 0.25
                                guicenter [ guifont "super" [ guitext (format "%1%2" (? $hi "^fd" "^fw") (+ $w2 1)) ] ]
                                guistrut 0.5
                                guilist [
                                    if (= $w3 0) [ guibackground 0xFFFFFF 0.01 0xFFFFFF 1 1 ]
                                    guistrut 0.125
                                    guiimage [textures/question] [loadweaps @w2 0] 1 0 [textures/blank] [] (? (= $w3 0) 0xFFFFFF 0x808080)
                                    guistrut 0.125
                                ] 
                                loop w1 $weapidxloadout [
                                    w4 = (+ $w1 $weapidxoffset)
                                    w5 = (at $weapname $w4)
                                    guilist [
                                        if (= $w3 $w4) [ guibackground 0xFFFFFF 0.01 $[@[w5]colour] 1 1 ]
                                        guistrut 0.125
                                        guiimage (? (allowedweap $w4) [textures/weapons/@w5] [textures/warning]) [loadweaps @w2 @w4] 1 0 [textures/blank] [] (? (= $w3 $w4) $[@[w5]colour] 0x808080)
                                        guistrut 0.125
                                    ] 
                                ]
                            ]
                            if $al [ gdw = (+ $gdw 1) ]
                        ]
                    ]
                ]
            ]
            guistrut 2
            guilist [
                guispring 1
                guicenter [ guitext "select favourite weapons" ]
                guistrut 0.5
                guifont "little" [
                    guicenter [ guitext (format "you can carry ^fs^fc%1^fS %2" $maxcarry (? (= $maxcarry 1) "weapon" "weapons")) ]
                ]
                guistrut 0.5
                guifont "little" [
                    guicenter [ guitext "^faif a weapon is not available, the" ]
                    guicenter [ guitext "^fanext one in the list will be used" ]
                ]
                guispring 1
                guifont "little" [
                    guicheckbox "show all slots" loadweapall
                    guilist [
                        guicheckbox "save for future use" loadweapsave
                        if (listlen $favloadweaps) [
                            guistrut 1
                            guistayopen [ guibutton "[^fs^frreset^fS]" [ favloadweaps "" ] ]
                        ]
                    ]
                    guicheckbox "automatically use saved loadout" autoloadweap 1 0 [
                        if (&& $autoloadweap (listlen $favloadweaps)) [ loadweapsave = 1 ]
                    ]
                ]
                guispring 1
                guicenter [ guifont "super" [ 
                    if (needloadout) [guibutton "^fomain menu" [showgui main]] [guibutton "^focancel" [cleargui 1]]
                    guispring 1
                    guibutton "^fgsubmit" [loadweap $loadweaplist $loadweapsave]
                ] ]
            ]
        ]
    ]
    cases (at $guirolloveraction 0) "loadweaps" [
        curweap = (at $guirolloveraction 2)
        if (> $curweap 0) [
            curname = (at $weapname $curweap)
            guitooltip (format "^f[%1]%2" $[@[curname]colour] $[@[curname]name])
        ] [ guitooltip "random weapon" ]
    ]
    guitip (format "press %1 to open this menu at any time" (dobindsearch "showgui loadout"))
] [
    if (= $guipasses 0) [
        loop i $weapidxloadout [ loadweaplist = (? $i (concat $loadweaplist (getloadweap $i)) (getloadweap $i)) ]
        loadweapsave = 0
    ]
]

newgui team [
    guione [
        guifont "default" [ guicenter [ guitext "choose team" ] ]
        guistrut 1
        if (isspectator (getclientnum)) [
            guicenter [ guibutton (format "^fw^f(%1) unspectate" $playertex) [spectator 0] ]
        ] [
            guicenter [ guibutton (format "^fw^f(%1) spectate" $spectatortex) [spectator 1] ]
        ]
        if (&& [>= (gamemode) $modeidxstart] [! (& (mutators) $mutsbitcoop)] (|| [! (& (mutators) $mutsbitffa)] [& (mutators) $mutsbitmulti])) [
            guistrut 1
            loop t (? (& (mutators) $mutsbitmulti) 4 2) [
                push t (at "alpha omega kappa sigma" $t) [
                    guicenter [ guibutton (format "^f[%1]^f(%2) %3" $[team@[t]colour] $[team@[t]tex] $[team@[t]name]) [team @t] ]
                ]
            ]
        ]
        guistrut 1
        guifont "default" [ guicenter [ guibutton "^focancel" [cleargui 1] [] "action" ] ]
    ]
    guitip (format "press %1 to open this menu at any time" (dobindsearch "showgui team"))
]

clientguitarget = -1
newgui client [
    clempty = (! (getclientpresence $clientguitarget))
    clname = (? $clempty "empty" (getclientname $clientguitarget))
    clcname = (? $clempty "empty" (getclientname $clientguitarget 1))
    clmodel = (? $clempty 0 (getclientmodel $clientguitarget))
    clcolour = (? $clempty 0 (getclientcolour $clientguitarget))
    clteam = (? $clempty 0 (getclientteam $clientguitarget))
    clweap = (? $clempty 0 (getclientweapselect $clientguitarget))
    clvanity = (? $clempty "" (getclientvanity $clientguitarget))
    cltab = (format "\f[%1]%2" (? $clempty 0x111111 $clcolour) $clientguitarget)
    guibox [guiplayerpreview $clmodel $clcolour $clteam $clweap $clvanity [cleargui 1] 7.5 1 1] [
        guicenter [ guifont "emphasis" [ guitext "player name" ] ]
        guicenter [ guitext $clcname ]
        //if (! $clempty) [
        guistrut 1
        guicenter [ guitext "quin got bored at this point" ]
    ]
] [ clientguitarget = $scoretarget ]