loadweaplist = ""
loadweapsave = 0
loadweaprespawn = 1; setpersist loadweaprespawn 1
loadweapall = 0; setpersist loadweapall 1

loadweaps = [
    lwa = ""
    lwo = (? (> (listlen $loadweaplist) $arg1) (at $loadweaplist $arg1) 0)
    loop w2 (min (listlen $loadweaplist) $weapidxloadout) [
        if (= $w2 $arg1) [
            lwa = (? $w2 (concat $lwa $arg2) $arg2)
        ] [
            w3 = (? (> (listlen $loadweaplist) $w2) (at $loadweaplist $w2) 0)
            if (&& $w3 (= $arg2 $w3)) [ w3 = $lwo ]
            lwa = (? $w2 (concat $lwa $w3) $w3)
        ]
    ]
    if (listlen $lwa) [ loadweaplist = $lwa ]
]

newgui loadout [
    guione [
        guifont "reduced" [ guicenter [ guitext "choose loadout weapons" ] ]
        guistrut 0.5
        guifont "reduced" [
            guicenter [
                guilist [
                    guiright [ guitext "^fa^f(textures/question)random" ]
                    loop w1 $weapidxloadout [
                        w4 = (+ $w1 $weapidxoffset)
                        push w1 (at $weapname $w4) [
                            guiright [ guitext (format "^f[%1]^f(%2)%3" $[@[w1]colour] [textures/weapons/@w1] $[@[w1]name]) ]
                        ]
                    ]
                ]
                guistayopen [
                    cnt = (? $loadweapall $weapidxloadout $maxcarry)
                    loop w2 $cnt [
                        w3 = (? (> (listlen $loadweaplist) $w2) (at $loadweaplist $w2) 0)
                        guistrut 0.5
                        guilist [
                            hi = (> (+ $w2 1) $maxcarry)
                            guiimage (? (= $w3 0) (? $hi "textures/checkboxtwo" "textures/checkboxon") "textures/checkbox") [loadweaps @w2 0] 0.5 0 "textures/blank"
                            loop w1 $weapidxloadout [
                                w4 = (+ $w1 $weapidxoffset)
                                if (allowedweap $w4) [
                                    guiimage (? (= $w3 $w4) (? $hi "textures/checkboxtwo" "textures/checkboxon") "textures/checkbox") [loadweaps @w2 @w4] 0.5 0 "textures/blank"
                                ] [ guinohitfx [ guiimage "textures/checkdisable" [] 0.5 0 "textures/blank" ] ]
                            ]
                        ]
                    ]
                ]
            ]
        ]
        guistrut 0.5
        guicenter [ guicenter [
            guifont "little" [
                guicenter [ guicheckbox "respawn when ready" loadweaprespawn ]
                guistrut 0.5
                guicenter [ guicheckbox "show all slots" loadweapall ]
                guicenter [
                    guicenter [ guicheckbox "save for future use" loadweapsave ]
                    if (listlen $favloadweaps) [
                        guistrut 1
                        guicenter [ guistayopen [ guibutton "[^fs^frreset^fS]" [ favloadweaps "" ] ] ]
                    ]
                ]
                guicenter [ guicheckbox "automatically use saved loadout" autoloadweap 1 0 [
                    if (&& $autoloadweap (listlen $favloadweaps)) [ loadweapsave = 1 ]
                ] ]
            ]
        ] ]
        guistrut 0.4
        guicenter [ guifont "super" [ 
            if (needloadout) [guibutton "^fomain menu" [showgui main]] [guibutton "^focancel" [cleargui 1]]
            guispring 1
            guibutton "^fgsubmit" [loadweap $loadweaplist $loadweapsave $loadweaprespawn]
        ] ]
    ]
    guitip (format "press %1 to open this menu at any time" (dobindsearch "showgui loadout"))
] [
    if (= $guipasses 0) [
        loop i $weapidxloadout [ loadweaplist = (? $i (concat $loadweaplist (getloadweap $i)) (getloadweap $i)) ]
        loadweapsave = 0
    ]
]

newgui team [
    guione [
        guifont "default" [ guicenter [ guitext "choose team" ] ]
        guistrut 1
        if (!= (getplayerstate) 4) [
            guicenter [ guibutton (format "^fw^f(%1) spectate" $spectatortex) [spectator 1] ]
            guistrut 1
        ]
        guifont "reduced" [
            if (&& [>= (gamemode) $modeidxstart] [! (& (mutators) $mutsbitcoop)] (|| [! (& (mutators) $mutsbitffa)] [& (mutators) $mutsbitmulti])) [
                loop t (? (& (mutators) $mutsbitmulti) 4 2) [
                    push t (at "alpha omega kappa sigma" $t) [
                        guicenter [ guibutton (format "^f[%1]^f(%2) %3" $[team@[t]colour] $[team@[t]tex] $[team@[t]name]) [team @t] ]
                    ]
                ]
            ] [
                guicenter [ guitext "there are no teams you can join" ]
            ]
        ]
        guistrut 1
        guifont "default" [ guicenter [ guibutton "^focancel" [cleargui 1] [] "action" ] ]
    ]
    guitip (format "press %1 to open this menu at any time" (dobindsearch "showgui team"))
]

clientguitarget = -1
newgui client [
    clempty = (! (getclientpresence $clientguitarget))
    clname = (? $clempty "empty" (getclientname $clientguitarget))
    clcname = (? $clempty "empty" (getclientname $clientguitarget 1))
    clmodel = (? $clempty 0 (getclientmodel $clientguitarget))
    clcolour = (? $clempty 0 (getclientcolour $clientguitarget))
    clteam = (? $clempty 0 (getclientteam $clientguitarget))
    clweap = (? $clempty 0 (getclientweapselect $clientguitarget))
    clvanity = (? $clempty "" (getclientvanity $clientguitarget))
    cltab = (format "\f[%1]%2" (? $clempty 0x111111 $clcolour) $clientguitarget)
    guibox [guiplayerpreview $clmodel $clcolour $clteam $clweap $clvanity [cleargui 1] 7.5 1 (? (strcmp $versionuname "mekarcade") 0.75 1)] [
        guicenter [ guifont "emphasis" [ guitext "player name" ] ]
        guicenter [ guitext $clcname ]
        //if (! $clempty) [
        guistrut 1
        guicenter [ guitext "quin got bored at this point" ]
    ]
] [ clientguitarget = $scoretarget ]