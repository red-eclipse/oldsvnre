macro resbutton [
    guibutton "%1x%2" "screenres %1 %2" [] (? (&& [= $scr_w %1] [= $scr_h %2]) "radioboxon" "radiobox")
]

newgui options [
    guiautotab 15
    guitext "performance key: ^fgfast^f~, ^fymoderate^f~, ^foslow and pretty^f~"
    guibar
    if (&& [< $shaders 0] [= $renderpath 0]) [
        guicheckbox "^fyshaders         "           shaders 1 -1
    ] [
        guilist [
            guicheckbox "^fyshaders			"           shaders
            if $shaders [
                guibar
                guiradio "^fglow detail " shaderdetail 1
                guiradio "^fyhigh detail " shaderdetail 3
                //if $hasglsl [
                //    guicheckbox "^foGLSL only" forceglsl
                //]
            ]
        ]
    ]
    guilist [
        guitext "water			" blank
        guibar
        guicheckbox "^fyrefraction "  waterrefract
        guicheckbox "^foreflection "  waterreflect
        guicheckbox "^fgcaustics "    caustics
    ]
    if (> $renderpath 0) [
        guilist [
            guitext "waterfalls			" blank
            guibar
            guicheckbox "^fyrefraction " waterfallrefract
            guicheckbox "^fgreflection " waterfallenv
        ]
    ]
    if (= $renderpath 0) [
        guilist [
            guicheckbox "^foshadow maps		"  shadowmap
            if $shadowmap [
                guibar
                guiradio "^fymedium quality " shadowmapsize 9 [blurshadowmap 1]
                guiradio "^fohigh quality " shadowmapsize 10 [blurshadowmap 2]
            ] [
                guibar
                guicheckbox "^fgblob shadows " blobs
            ]
        ]
        if (>= $maxtmus 3) [
            guicheckbox "^fydynamic lights"    ffdynlights 5 0
        ]
    ] [
        guilist [
            guicheckbox "^fosoft shadows		"  shadowmap
            if $shadowmap [
                guibar
                guiradio "^fymedium quality " shadowmapsize 9 [blurshadowmap 1]
                guiradio "^fohigh quality " shadowmapsize 10 [blurshadowmap 2]
            ] [
                guibar
                guicheckbox "^fgblob shadows " blobs
            ]
        ]
        if $glare [
            glarepreset = 0
            if (= $glarescale 1) [
                if (= $blurglare 4) [glarepreset = 1]
                if (= $blurglare 7) [glarepreset = 3]
            ]
            if (= $glarescale 2) [
                if (= $blurglare 3) [glarepreset = 2]
                if (= $blurglare 7) [glarepreset = 4]
            ]
            guilist [
                guicheckbox "^foglare				"       glare
                guibar
                guiradio "^fysubtle " glarepreset 1 [blurglare 4; glarescale 1]
                guiradio "^fyglowy " glarepreset 2 [blurglare 3; glarescale 2]
                guiradio "^fosoft " glarepreset 3 [blurglare 7; glarescale 1]
                guiradio "^fointense " glarepreset 4 [blurglare 7; glarescale 2]
            ]
        ] [
            guicheckbox "^foglare"         glare
        ]
    ]
    if $usetexrect [
        guilist [
            guicheckbox "^fomotion blur		" motionblur
            if $motionblur [
                guibar
                guiradio "^fosubtle " motionblurscale 0.5
                guiradio "^fomoderate " motionblurscale 1
            ]
        ]
    ]
    guilist [
        guicheckbox "^fograss			" grass
        if $grass [
            guibar
            guiradio "^fyquick fade " grassdist 128
            guiradio "^fymoderate fade " grassdist 256
            guiradio "^foslow fade " grassdist 512
        ]
    ]
    if (> $renderpath 0) [
        guilist [
            guicheckbox "^fgdynamic lights		"    maxdynlights 3 0
            if $maxdynlights [
                guibar
                guiradio "^fgmedium quality " maxdynlights 3
                guiradio "^fyhigh quality " maxdynlights 5
            ]
        ]
        guilist [
            guicheckbox "^fysoft particles		" depthfx
            if $depthfx [
                guibar
                guiradio "^fglow quality " depthfxsize 7 [depthfxrect 0; depthfxfilter 1; blurdepthfx 1]
                guiradio "^fymedium quality " depthfxsize 10 [depthfxrect 1; depthfxfilter 0; blurdepthfx 0]
                guiradio "^fohigh quality " depthfxsize 12 [depthfxrect 1; depthfxfilter 0; blurdepthfx 0]
            ]
        ]
    ]
    guicheckbox "^fgglass reflection"  glassenv
    guilist [
        guicheckbox "^fgdecals			" decals
        if $decals [
            guibar
            guiradio "^fgmedium quality " maxdecaltris 1024
            guiradio "^fyhigh quality " maxdecaltris 4096
        ]
    ]
    guicheckbox "^fgfix t-joints (world sparklies)" filltjoints
    guilist [
        guitext "textures			" blank
        guibar
        guiradio "^fglow quality " maxtexsize 256
        guiradio "^fgmedium quality" maxtexsize 512
        guiradio "^fyhigh quality " maxtexsize 0
    ]
    guilist [
        guitext "models			" blank
        guibar
        guicheckbox "^fglighting "    lightmodels
        guicheckbox "^fgreflection "  envmapmodels
        guicheckbox "^fgglow "        glowmodels
        if (> $renderpath 0) [
            guicheckbox "^fybumpmap " bumpmodels
        ]
    ]
    guilist [
        guitext "animation			" blank
        guibar
        guiradio "^fgmedium quality " matskel 1
        guiradio "^fghigh quality " matskel 0
    ]

    guitab    "display"
    guicheckbox "v-sync"            vsync 1 0
    guicheckbox "fullscreen"        fullscreen
    guitext "gamma (default: 100)"
    guislider gamma
    guitext "full-scene anti-aliasing (default: -1)"
    guilistslider fsaa "-1 0 2 4 8 16"
    guitext "color depth (default: 0)"
    guilistslider colorbits "0 8 16 24 32"
    guitext "z-buffer depth (default: 0)"
    guilistslider depthbits "0 8 16 24 32"
    guitext "stencil bits (default: 0)"
    guislider stencilbits
    guitext "anisotropic filtering (default: 0)"
    guilistslider aniso "0 2 4 8 16"

    guitab   "ui"
    guilist [
        guitext "blinking text (default: 250ms, 0 = off): "
        guilist [
            guilist [ guistrut 60 ]
            guilistslider blinkingtext "0 50 100 150 200 250 300 350 400 450 500 550 600 650 700 750 800 850 900 950 1000"
        ]
    ]
    guibar
    guilist [
        guicheckbox "show crosshair " showcrosshair
        guicheckbox "weapon crosshairs " crosshairweapons
        guicheckbox "show clips " showclips
        guicheckbox "show radar " showradar
    ]
    guibar
    guilist [
      guitext "crosshair size (0 - 1000, default: 0.05): "
      crosshairsizestorage = $crosshairsize
      guifield crosshairsizestorage 6 [crosshairsize $crosshairsizestorage]
      guistrut 1
      guitext "zoom crosshair size (0 - 1000, default: 0.575): "
      zoomcrosshairsizestorage = $zoomcrosshairsize
      guifield zoomcrosshairsizestorage 6 [zoomcrosshairsize $zoomcrosshairsizestorage]
    ]
    guibar
    guitext "notice messages"
    guilist [
    guilist [
      guiradio "off" shownotices 0
      guiradio "basic notices" shownotices 1
      guiradio "level 2" shownotices 2
      guiradio "level 3" shownotices 3
      guiradio "everything" shownotices 4
    ]
    guibar
    guilist [
      guitext "fps display"
      guiradio "off" showfps 0
      guiradio "number only" showfps 1
      guiradio "verbose" showfps 2
      guiradio "more verbose" showfps 3
    ]
    ]

    guitab "res"
    guistayopen [
      guilist [
        guilist [
            guitext "4:3"
            @(resbutton 320 240)
            @(resbutton 640 480)
            @(resbutton 800 600)
            @(resbutton 1024 768)
            @(resbutton 1152 864)
            @(resbutton 1280 960)
            @(resbutton 1400 1050)
            @(resbutton 1600 1200)
            @(resbutton 1792 1344)
            @(resbutton 1856 1392)
            @(resbutton 1920 1440)
            @(resbutton 2048 1536)
            @(resbutton 2800 2100)
            @(resbutton 3200 2400)
        ]
        guibar
        guilist [
            guitext "16:10"
            @(resbutton 320 200)
            @(resbutton 640 400)
            @(resbutton 1024 640)
            @(resbutton 1280 800)
            @(resbutton 1440 900)
            @(resbutton 1600 1000)
            @(resbutton 1680 1050)
            @(resbutton 1920 1200)
            @(resbutton 2048 1280)
            @(resbutton 2560 1600)
            @(resbutton 3840 2400)
        ]
        guibar
        guilist [
            guitext "16:9"
            @(resbutton 1024 600)
            @(resbutton 1280 720)
            @(resbutton 1366 768)
            @(resbutton 1600 900)
            @(resbutton 1920 1080)
            @(resbutton 2048 1152)
            @(resbutton 3840 2160)
        ]
        guibar
        guilist [
            guitext "5:4"
            @(resbutton 600 480)
            @(resbutton 1280 1024)
            @(resbutton 1600 1280)
            @(resbutton 2560 2048)
        ]
        guibar
        guilist [
            guitext "5:3"
            @(resbutton 800 480)
            @(resbutton 1280 768)

            guibar
            guitext "Custom"
            guilist [
                customw = $scr_w
                customh = $scr_h
                guifield customw 4 [scr_w $customw]
                guifield customh 4 [scr_h $customh]
            ]
        ]
      ]
    ]

    guitab   "sound"
    guitext "master volume"
    guislider mastervol
    guitext "sound volume"
    guislider soundvol
    guitext "music volume"
    guislider musicvol
    guitext "sound channels"
    guislider soundchans
    guitext "sound frequency"
    guilistslider soundfreq "11025 22050 44100"
    guitext "sound buffer length"
    guislider soundbufferlen
    guilist [
            guilist [
                guitext "in-game music		"
            ]
            guilist [
                guilist [
                    guiradio "off	" 		musictype 0
                    guiradio "mapmusic	" 	musictype 1
                    guiradio "random	" 	musictype 2
                    ]
                ]
            ]
    guilist [
            guilist [
                guitext "edit mode music	"
            ]
            guilist [
                guilist [
                    guiradio "off	" 		musicedit 0
                    guiradio "mapmusic	" 	musicedit 1
                    guiradio "random	" 	musicedit 2
                    ]
                ]
            ]
    guitext
    guicheckbox "mumble positional audio" mumble

    guitab  "mouse"
    guilist [
        guilist [
            guitext "mouse overall sensitivity  "
            guitext "mouse yaw sensitivity"
            guitext "mouse pitch sensitivity"
            guistrut 1
            guicheckbox "invert y-axis" mouseinvert
        ]
        guilist [
            guistrut 50 1
            guislider sensitivity 1 100
            guislider yawsensitivity 0 100
            guislider pitchsensitivity 1 100
        ]
    ]

    guitab  "keys"
    guitext "basic keybinds, for anything more use the 'bind' command"
    guitext "select action to bind and press desired keys (ESC when done):"
    guistrut 1
    guilistsplit n 2 $bindactions [
        guilist [
            guitext (tabify (concatword (at $bindnames $gli) " ") 4)
            [newbinds@gli] = (searchbinds $n)
            guikeyfield [newbinds@gli] -24 [
                oldbinds = (searchbinds [@@n])
                looplist j $oldbinds [bind $j ""]
                looplist j $[newbinds@@gli] [bind $j [@@@n]]
            ]
        ]
    ] [guistrut 2]

    guitab "autoexec.cfg"
    guitext "autoexec.cfg"
    showfileeditor "autoexec.cfg" -80 20
]

newgui postfx [
    guibutton "(effect OFF)"          "clearpostfx"
    guibutton "bloom (subtle: 30%)"    "bloom 0.3"
    guibutton "bloom (bright: 55%)"    "bloom 0.55"
    guibutton "bloom (intense: 80%)"  "bloom 0.8"
    guibutton "rotoscope" "rotoscope 1"
    guibutton "rotoscope + blur3" "rotoscope 1 1"
    guibutton "rotoscope + blur5" "rotoscope 1 2"
    guibutton "sobel"  "setpostfx sobel"
    guibutton "invert" "setpostfx invert"
    guibutton "gbr"    "setpostfx gbr"
    guibutton "bw"     "setpostfx bw"
    guibutton "blur3"  "setpostfx hblur3; addpostfx vblur3"
    guibutton "blur5"  "setpostfx hblur5; addpostfx vblur5"
]

bindactions = [
    forward backward left right "universaldelta 1" "universaldelta -1" "spectator 1" "spectator 0"
    "action 0" "action 1" "action 2" "action 3" "action 4" "action 5" "action 6" "action 7" "action 8" "action 9"
    "weapon 0" "weapon 1" "weapon 2" "weapon 3" "weapon 4" "weapon 5" "weapon 6" "weapon 7" "weapon 8" "weapon 9"
    "saycommand /" saytextcommand sayteamcommand toggleconsole edittoggle takescreenshot
    addbot delbot "showgui maps 1" "showgui maps 2" "setpriv 1" "showgui loadout" "showgui team"
    "showcompass voice" "showcompass team"
]
bindnames = [
    forward backward left right "scroll up" "scroll down" "enter spectator" "exit spectator"
    "attack" "alt-attack" "reload" "use" "jump" "run/walk" "crouch" "special" "drop" "affinity"
    "melee" "pistol" "sword" "shotgun" "smg" "flamer" "plasma" "rifle" "grenade" "rocket"
    "cmd input" "all chat" "team chat" "toggle console" "toggle editing" "screenshot"
    "add bot" "delete bot" "maps menu" "maps voting" "claim privileges" "loadout menu" "team menu"
    "voice compass" "team compass"
]
