resetmapgui = 0
shownservergui = 0
mapselected = 0
mutsselected = 0
modeselected = -1
mutsbefore = 0
lastmode = -1
lastmuts = 0

maplists = [ campaignmaps mainmaps capturemaps defendmaps bombermaps trialmaps ]
mapindex = 0
lastmode = -1
lastmuts = 0
mapnum = -1
mapcur = ""
mappth = ""
maplist = ""
mappath = ""
mapextra = ""
mapimg = ""
mapocta = 0
mapfavs = ""; setpersist mapfavs 1
demofavs = ""; setpersist demofavs 1
resetmapgui = 0

mapsmenuinit = [
    modeselected = -1
    mutsselected = 0
    mapindex = 0
    lastmode = -1
    lastmuts = 0
    mapnum = -1
    mapcur = ""
    mappth = ""
    maplist = ""
    mappath = ""
    mapextra = ""
    mapimg = ""
    mapocta = 0
    resetmapgui = 1
    mutsbefore = $mutsselected
]

mapsmenuiter = [
    if (|| [!= $lastmode $modeselected] [!= $lastmuts $mutsselected]) [ resetmapgui = 1 ]
    if (= $resetmapgui 1) [
        maprot = 0
        maplist = ""
        mappath = ""
        if (= $modeselected $modeidxdemo) [
            mutsselected = 0
            loop q 2 [
                loopfiles lcurmap demos dmo [
                    if (? $q (< (listfind xcurmap $demofavs [strcmp $xcurmap $lcurmap]) 0) (>= (listfind xcurmap $demofavs [strcmp $xcurmap $lcurmap]) 0)) [
                        append maplist $lcurmap
                        append mappath [demos/@lcurmap]
                    ]
                ]
            ]
            maprot = (listlen $maplist)
            append maplist "?"
            append mappath "?"
        ] [
            mutsselected = (mutscheck $modeselected $mutsselected)
            if (>= $modeselected $modeidxplay) [
                curlist = (getmaplist $modeselected $mutsselected (? (isonline) (listlen (listclients 1)) 0))
                curprev = (sublist $previousmaps 0 $maphistory)
                loop q 2 [
                    looplist lcurmap $curlist [
                        if (< (listfind pcurmap $curprev [strcmp $pcurmap $lcurmap]) 0) [
                            if (? $q (< (listfind xcurmap $mapfavs [strcmp $xcurmap $lcurmap]) 0) (>= (listfind xcurmap $mapfavs [strcmp $xcurmap $lcurmap]) 0)) [
                                append maplist $lcurmap
                                append mappath [maps/@lcurmap]
                            ]
                        ]
                    ]
                ]
                maprot = (listlen $maplist)
                wantlist = 1
                looplist lcurmap $curprev [
                    if $wantlist [
                        append maplist "~"
                        append mappath "~"
                        wantlist = 0
                    ]
                    append maplist $lcurmap
                    append mappath [maps/@lcurmap]
                ]
            ] [ maprot = 0 ]
            wantlist = 1
            loop q 2 [
                loopfiles lcurmap maps mpz [
                    if (< (listfind mcurmap $maplist [strcmp $mcurmap $lcurmap]) 0) [
                        if (? $q (< (listfind xcurmap $mapfavs [strcmp $xcurmap $lcurmap]) 0) (>= (listfind xcurmap $mapfavs [strcmp $xcurmap $lcurmap]) 0)) [
                            if $wantlist [
                                append maplist "*"
                                append mappath "*"
                                wantlist = 0
                            ]
                            append maplist $lcurmap
                            append mappath [maps/@lcurmap]
                        ]
                    ]
                ]
            ]
            if (&& [> $mapocta 0] [> $hasoctapaks 0]) [
                wantlist = 1
                loop q 2 [
                    loopfiles lcurmap base ogz [
                        if (? $q (< (listfind xcurmap $mapfavs [strcmp $xcurmap $lcurmap]) 0) (>= (listfind xcurmap $mapfavs [strcmp $xcurmap $lcurmap]) 0)) [
                            if $wantlist [
                                append maplist "."
                                append mappath "."
                                wantlist = 0
                            ]
                            append maplist $lcurmap
                            append mappath [base/@lcurmap]
                        ]
                    ]
                ]
            ]
            append maplist "?"
            append mappath "?"
            mapnum = (listfind curmap $maplist [
                || [=s $curmap $mapcur] [=s [maps/@curmap] $mapcur] [
                    && [> $hasoctapaks 0] [> $mapocta 0] [=s [base/@curmap] $mapcur]
                ]
            ])
        ]
        mapcount = (listlen $maplist)
        if (! $maprot) [ maprot = $mapcount ]
        lastmode = $modeselected
        lastmuts = $mutsselected
        mapindex = 0
        resetmapgui = 0
    ]
    if (|| [< $mapnum 0] [>= $mapnum $mapcount]) [
        mapnum = -1
        mapcur = ""
        mappth = ""
        mapdmo = ""
        mapimg = ""
    ] [
    if (=s (at $maplist $mapnum) "?") [
        mapcur = $mapextra
        if (= $modeselected $modeidxdemo) [
            mappth = [demos/@mapcur]
            mapdmo = (demoscan (format "%1.dmo" $mappth))
            mapimg = (? (>= $mapdmo 0) (format "maps/%1" (demoinfo $mapdmo 2)) "textures/emblem")
        ] [
            mappth = $mapextra
            mapimg = $mapextra
        ]
    ] [
        mapcur = (at $maplist $mapnum)
        mappth = (at $mappath $mapnum)
        if (= $modeselected $modeidxdemo) [
            mapdmo = (demoscan (format "%1.dmo" $mappth))
            mapimg = (? (>= $mapdmo 0) (format "maps/%1" (demoinfo $mapdmo 2)) "textures/emblem")
        ] [
            mapimg = (at $mappath $mapnum)
        ]
    ] ]
]

mutsvar = [
    local g m s t
    guistayopen [ guilist [
        g = $$arg2
        m = $$arg3
        s = $$arg5
        if (& (mutsimplied $g $m) $arg4) [
            guibutton $arg1 [ implied = @arg4 ] [] "checkboxtwo"
        ] [
            if (|| (& (mutscheck $g (| $m $arg4) $arg4) $arg4) (! (ismodelocked $g $m $arg4 $s))) [
                t = (& $m $arg4)
                guibutton $arg1 [
                    mutate = @arg4
                    if @t [@@arg3 = @@(mutscheck $g (- $m $arg4))] [
                        @@arg3 = @@(mutscheck $g (| $m $arg4) $arg4)
                    ]
                ] [] (? $t "checkboxon" "checkbox")
            ] [ guibutton $arg1 [ disabled = @arg4 ] [] "checkdisable" ]
        ]
    ] ]
]

modevar = [
    local g m s
    guistayopen [ guilist [
        g = $arg4
        m = $$arg3
        s = $$arg5
        if (ismodelocked $g $m 0 $s) [ guibutton $arg1 [ disabled = @arg4 ] [] "checkdisable" ] [
            guibutton $arg1 [@arg2 = @arg4] [] (? (= $$arg2 $arg4) "checkboxon" "checkbox")
        ]
    ] ]
]

mapsexec = [
    sleep 1 [
        if (isconnected) [ showgui maps 2 ]
        start @arg1 @arg2 @arg3
    ]
]

mapsmenu = [
    guilist [
        guistrut 74 1
        guitip (format "press %1 to open this menu at any time" (dobindsearch "showgui maps 1"))
        guistrut 0.5
        guilist [
            guilist [
                guilist [
                    guistrut 3 1
                    guilist [
                        guistrut 32 1
                        if (< $lastmode 0) [
                            guitext "game select"
                            guifont "little" [ guitext "please select a mode and map to continue" ]
                        ] [
                            gname = (gamename $modeselected $mutsselected 0 32)
                            guilist [
                                guitext $gname
                                if (= $modeselected $modeidxdemo) [
                                    dinfo = (demoscan (format "%1.dmo" $mappth))
                                    dmode = (demoinfo $dinfo 3)
                                    dmuts = (demoinfo $dinfo 4)
                                    dname = (gamename $dmode $dmuts 0 32)
                                    guifont "little" [ guitext (format " (^fs^fa%1^fS)" $dname) ]
                                ]
                            ]
                            guilist [
                                if (strlen $mapcur) [
                                    guitext " ^faselected on ";
                                    guitext $mapcur
                                    if (= $modeselected $modeidxdemo) [
                                        dinfo = (demoscan (format "%1.dmo" $mappth))
                                        dmapname = (demoinfo $dinfo 2)
                                        guifont "little" [ guitext (format " (^fs^fa%1^fS)" $dmapname) ]
                                    ]
                                ] [ guifont "little" [ guitext "please select a map to continue" ] ]
                            ]
                        ]
                        guistrut 0.25
                        guifont "little" [
                            guilist [
                                guispring
                                guistayopen [ guibutton "^fvpick random" [ modeselected = (+ (rnd (- $modeidxnum $modeidxplay)) $modeidxplay); mutsselected = (mutscheck $modeselected (rnd $mutsbitall)) ] ]
                                guispring
                                if (isconnected) [
                                    guistayopen [
                                        guibutton "^focopy current" [
                                            modeselected = (gamemode)
                                            if (< (mutators) 0) [ mutsselected = 0 ] [ mutsselected = (mutscheck $modeselected (mutators)) ]
                                        ]
                                    ]
                                    guispring
                                ]
                                guistayopen [ guibutton "^fyreset selection" [ modeselected = -1; mutsselected = 0; mapnum = -1; mapcur = ""; mappth = ""; mapimg = ""; mapdmo = "" ] ]
                                guispring
                            ]
                        ]
                        guistrut 0.25
                    ]
                ]
                guilist [
                    guilist [
                        guitext "mode"
                        guistrut 0.5
                        guifont "little" [
                            loop z $modeidxnum [
                                modevar (at $modename $z) modeselected mutsselected $z mapcur
                            ]
                        ]
                    ]
                    guistrut 3
                    guilist [
                        guistayopen [
                            guiimage $mapimg [if (>= @lastmode 0) [ mapsexec @mapcur @modeselected @mutsselected ]] 5 1 "textures/emblem"
                        ]
                    ]
                ]
                guistrut 0.5
                guifont "little" [ guitext "mutators" ]
                guistrut 0.5
                guifont "little" [
                    cnt = (- $mutsidxnum $mutsidxgsn)
                    guiloopsplit n 3 $cnt [
                        mutsvar (at $mutsname $n) modeselected mutsselected (<< 1 $n) mapcur
                    ] [ guistrut 3 ]
                ]
                if (> (strlen (gspmutname $modeselected 0)) 0) [
                    guistrut 0.5
                    guifont "little" [ guitext "mode specific" ]
                    guistrut 0.5
                    guifont "little" [
                        guiloopsplit n 3 $mutsidxgsn [
                            mut = (gspmutname $modeselected $n)
                            if (strlen $mut) [
                                mutsvar $mut modeselected mutsselected (<< 1 (+ $mutsidxgsp $n)) mapcur
                            ]
                        ] [ guistrut 3 ]
                    ]
                ]
            ]
            guistrut 1
            mapscount = 21
            guilist [
                guilist [
                    guifont "little" [ guitext (? (>= $lastmode 0) "available maps:" "^fawaiting for mode/mutators..") ]
                    if (> $hasoctapaks 0) [
                        guispring
                        guistayopen [
                            guibutton "show sauer maps" [
                                mapocta = (! $mapocta)
                                resetmapgui = 1
                            ] [] (? (> $mapocta 0) "checkboxon" "checkbox")
                        ]
                        guispring 1
                        guitext "fav"
                        guistrut 3
                    ]
                ]
                guistrut 0.25
                guilist [
                    guifont "little" [
                        guicontainer [>= $lastmode 0] [
                            nummaps = (- (listlen $maplist) 1)
                            guilist [
                                guilist [
                                    guistrut $mapscount 1
                                    mapindex = (min (max 0 (- $nummaps $mapscount)) $mapindex) //safeguard
                                    mapnum = (min $mapnum $nummaps)
                                    guilist [
                                        guistrut 37 1
                                        break = 0
                                        loopwhile i $mapscount [= $break 0] [
                                            q = (+ $mapindex $i)
                                            curmap = (at $maplist $q)
                                            cases $curmap "*" [
                                                guifont "little" [ guitext "maps not in the rotation" ]
                                            ] "~" [
                                                guifont "little" [ guitext "maps played recently" ]
                                            ] "." [
                                                guifont "little" [ guitext "maps from sauerbraten" ]
                                            ] "?" [
                                                break = 1
                                            ] () [
                                                guilist [
                                                    guiradio $curmap mapnum $q
                                                    guispring 1
                                                    guistayopen [
                                                        hasmap = (>= (indexof (? (= $modeselected $modeidxdemo) $demofavs $mapfavs) $curmap) 0)
                                                        guiimage (? $hasmap "textures/checkboxon" "textures/checkbox") [
                                                            if (= $modeselected $modeidxdemo) [
                                                                demofavs = (? @@hasmap (listdel $demofavs @@curmap) (concat (listdel $demofavs @@curmap) @@curmap))
                                                            ] [
                                                                mapfavs = (? @@hasmap (listdel $mapfavs @@curmap) (concat (listdel $mapfavs @@curmap) @@curmap))
                                                            ]
                                                            resetmapgui = 1
                                                        ] 0.5 0 "textures/blank"
                                                    ]
                                                ]
                                            ]
                                        ]
                                    ]
                                ]
                                guilist [ guifont "little" [ guitext "custom map" ] ]
                                guilist [
                                    mapextraval = $mapextra
                                    mapextranum = $nummaps
                                    guiradio "" mapnum $mapextranum
                                    guifield mapextraval 36 [mapextra = $mapextraval; mapnum = $mapextranum]
                                ]
                            ]
                            guislider mapindex 0 (max (- $nummaps $mapscount) 0) [] 1 1
                        ] [
                            guistrut 40.25
                            guistrut (+f $mapscount 2) 1
                        ]
                    ]
                ]
            ]
        ]
        guistrut 0.25
        guilist [
            if (! (isonline)) [
                guifont "small" [
                    guitext "server type:"; guistrut 0.5
                    guiradio "offline" servertype 0; guistrut 0.5
                    guiradio "private" servertype 1; guistrut 0.5
                    guiradio "public" servertype 2
                ]
            ]
            guispring 1
            if (>= $lastmode 0) [
                if (strlen $mapcur) [
                    guifont "default" [
                        guistayopen [ guibutton (? (isonline) "^fgsubmit this vote" "^fgstart local game") [ mapsexec $mapcur $modeselected $mutsselected ] ]
                    ]
                    guistrut 13
                ] [ guifont "default" [ guitext "^fy.. pick a map to continue .." ]; guistrut 9 ]
            ] [ guifont "default" [ guitext "^fy.. pick a mode and map to continue .." ]; guistrut 4 ]
        ]
        guistrut 1
        guilist [
            guilist [
                guifont "small" [
                    act = (? (strlen $guirolloverimgaction) $guirolloverimgaction $guirolloveraction)
                    cases (at $act 0) "modeselected" [
                        guitext (format "%1" (modedesc (at $act 2) $mutsselected 3))
                    ] "mutate" [
                        guitext (format "%1" (mutsdesc $modeselected (at $act 2) 3))
                    ] "implied" [
                        guitext "this is implied in the current configuration"
                    ] "disabled" [
                        guitext "this is disabled in the current configuration"
                    ] () [guitext "hover over a mode or mutator to see its description"]
                ]
            ]
        ]
    ]
]

voteindex = 0
votenum = 0
votemenuinit = [ voteindex = 0 ]

votemenu = [
    guipage vote 8 72 3.2 [1] (getvote) [
        voteplayers = (getvote $i 0)
        votemode = (getvote $i 1)
        votemuts = (getvote $i 2)
        votemap = (getvote $i 3)
        voteself = 0
        loop j $voteplayers [ if (= (getclientnum) (getvote $i 0 $j)) [ voteself = 1 ] ]
    ] [1] [
        guibutton "^fwThere are no votes currently pending, ^fgsubmit ^fwone yourself" [showgui maps 1] [] "chat"
    ] [
        guitip (format "press %1 to open this menu at any time" (dobindsearch "showgui maps 2"))
        guistrut 0.5
        guilist [
            guicheckbox "^fcdynamic sort" sortvotes; guistrut 1.5
            guicheckbox "^focleanup list" cleanvotes; guistrut 1.5
            guibutton "^fyclear vote" clearvote
            guispring
            guitext (format "^fd[ ^fc%1 ^fwvote%2 ^fd]" $votenum (? (!= $votenum 1) "s"))
            guistrut 1
        ]
        guistrut 0.25
    ] [
        guistrut 0.125
        guimerge 72 [
            guicenter [
                guistrut 4 1
                votecolour = (? $voteself "^fy" "^fw")
                guicenter [guifont "default" [guibutton (concatword $votecolour $voteplayers)]]
                guicenter [guifont "little" [guibutton (format "%1vote%2" $votecolour (? (!= $voteplayers 1) "s"))]]
            ]
            voteimage = "textures/chat"
            votepath = (listfind curmap $maplist [|| [=s $curmap $votemap] [=s [maps/@curmap] $votemap] [
                && [> $mapocta 0] [> $hasoctapaks 0] [=s [base/@curmap] $votemap]
            ]])
            if (> $votepath -1) [ voteimage = (at $mappath $votepath) ]
            guistrut 1
            guiimage $voteimage "" 1.5 1 "textures/chat"
            guistrut 1
            guicenter [
                gname = (gamename $votemode $votemuts 0 32)
                guifont "reduced" [ guibutton (format "^fy%1 ^favoted for on ^fo%2" $gname $votemap) ]
                guilist [
                    if (> $voteplayers 0) [
                        guifont "reduced" [ guibutton "^fwby " ]
                        guifont "little" [ 
                            pname = ""
                            pmore = 0
                            loop j $voteplayers [
                                if (> (strlen $pname) 40) [ pmore = (+ $pmore 1) ] [
                                    append pname (format ["%1"] (getclientname (getvote $i 0 $j) 1))
                                ]
                            ]
                            guibutton (concat (prettylist $pname) (? $pmore (concat "and^fy" $pmore "^fwmore")))
                        ]
                    ] [ guifont "reduced" [ guibutton "^fano current votes" ] ]
                ]
            ]
        ] [
            if (= @voteself 1) [ clearvote ] [ start @@votemap @@votemode @@votemuts ]
        ] []
        guistrut 0.125
    ]
]

newgui maps [
    octapaks [
        mapsmenuiter
        guilist [ mapsmenu ]
        guitab "votes"
        guilist [ votemenu ]
    ]
] [
    if (= $guipasses 0) [
        octapaks [ mapsmenuinit; votemenuinit ]
    ]
]
