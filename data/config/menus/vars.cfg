varsearchstr = ""
varscount = 30
varindex = 0
varnum = -1
vartypes = 0
varnotypes = 56
varflags = 0
varnoflags = 128

newgui vars [
    guilist [ guifield varsearchstrval 46 [varsearchstr = $varsearchstrval; varnum = -1; varindex = 0] ]
    guistrut 0.5
    numvars = (getvarinfo -1 $vartypes $varnotypes $varflags $varnoflags $varsearchstr)
    if (>= $varnum $numvars) [ varnum = -1 ]
    guilist [
        guilist [ guilist [
            guifont "little" [
                guicontainer [1] [
                    guilist [
                        guilist [
                            guistrut $varscount 1
                            varindex = (min (max 0 (- $numvars $varscount)) $varindex) //safeguard
                            varnum = (min $varnum $numvars)
                            guilist [
                                guistrut 40 1
                                break = 0
                                loopwhile i $varscount [= $break 0] [
                                    q = (+ $varindex $i)
                                    if (< $q $numvars) [
                                        curvar = (getvarinfo $q $vartypes $varnotypes $varflags $varnoflags $varsearchstr)
                                        guilist [ guiradio $curvar varnum $q ]
                                    ] [ guistrut 1 ]
                                ]
                            ]
                        ]
                    ]
                    guislider varindex 0 (max (- $numvars $varscount) 0) [] 1 1
                ] [
                    guistrut 43.25
                    guistrut (+f $varscount 1) 1
                ]
            ]
        ] ]
        guistrut 1
        guilist [
            guistrut 59 1
            if (&& (>= $varnum 0) (< $varnum $numvars) (> $numvars 0)) [
                scurvar = (getvarinfo $varnum $vartypes $varnotypes $varflags $varnoflags $varsearchstr)
                scurvartype = (getvartype $scurvar)
                guifont "emphasis" [ guitext $scurvar ]
                guilist [
                    case $scurvartype 0 [
                        guitext "^fainteger"
                        guispring 1
                        guitext (format "^famin: ^fw%1" (getvarmin $scurvar))
                        guispring 1
                        guitext (format "^famax: ^fw%1" (getvarmax $scurvar))
                        guispring 1
                        guitext (format "^fadefault: ^fw%1" (getvardef $scurvar 1))
                    ] 1 [
                        guitext "^fafloat"
                        guispring 1
                        guitext (format "^famin: ^fw%1" (getfvarmin $scurvar))
                        guispring 1
                        guitext (format "^famax: ^fw%1" (getfvarmax $scurvar))
                        guispring 1
                        guitext (format "^fadefault: ^fw%1" (getfvardef $scurvar 1))
                    ] 2 [
                        guitext "^fastring"
                    ] 3 [
                        guitext "^facommand"
                    ] 4 [
                        guitext "^faalias"
                    ] 5 [
                        guitext "^falocal"
                    ] () [
                        guitext "^fa???"
                    ]
                ]
                if (= $scurvartype 2) [
                    guistrut 0.25
                    guilist [
                        guitext "^fadefault"
                        guispring 1
                        guieditor [@[scurvar]_vardef] -58 8 4 0 0 "" (getsvardef $scurvar 1)
                    ]
                ]
                guistrut 0.25
                guilist [
                    guitext "value"
                    guispring 1
                    [@[scurvar]_varval] = $$scurvar
                    guifield [@[scurvar]_varval] -58 [@scurvar $[@@[scurvar]_varval]] 0 0 "" (? (|| (= $scurvartype 2) (= $scurvartype 4)) 8 1)
                ]
                scurusage = (getvarusage $scurvar)
                if (strlen $scurusage) [
                    guistrut 0.25
                    guilist [
                        guitext "^fausage"
                        guispring 1
                        guieditor [@[scurvar]_varusage] -58 2 4 0 0 "" $scurusage
                    ]
                ]
                scurdesc = (getvardesc $scurvar)
                if (strlen $scurdesc) [
                    guistrut 0.25
                    guilist [
                        guitext "^fainfo"
                        guispring 1
                        guieditor [@[scurvar]_vardesc] -58 8 4 0 0 "" $scurdesc
                    ]
                ]
            ] [
                guitext "no variable selected"
            ]
        ]
    ]
] [
    if (= $guipasses 0) [
        varindex = 0
        varnum = -1
    ]
]
