bind ESCAPE [if (cleargui 1) [] [showgui main]]    // it all starts here

newgui main [
    guione [
        guifont "emphasis" [
            guicenter [ guifont "huge" [ guitext "^f(textures/logo)" ] ]
            guistrut 0.5
            guicenter [ guibutton "profile" "showgui profile" ]
            guistrut 0.5
            if (isconnected) [
                if (= (gamemode) $modeidxediting) [
                    guicenter [ guibutton "editing" "showgui editing" ]
                    guistrut 0.5
                ]
                guicenter [ guibutton "disconnect" "disconnect" ]
                if (> (getvote) 0) [ guicenter [ guibutton "show votes" "showgui maps 2" ] ]
                guistrut 0.5
            ]
            guicenter [ guibutton (? (isonline) "find servers" "play online") "showservers" ]
            guicenter [ guibutton (? (isconnected) "vote map/mode" "offline practice") "showgui maps 1" ]
            guistrut 0.5
            if (> (ircconns) 0) [ guicenter [ guibutton "irc windows" "showgui irc" ] ]
            guicenter [ guibutton "options" "showgui options" ]
            guicenter [ guibutton "variables" "showgui vars" ]
            guicenter [ guibutton "help" "showgui help" ]
            guistrut 0.5
            guicenter [ guibutton "quit" "quit" ]
        ]
    ]
    cases (? (strlen $guirolloverimgaction) $guirolloverimgaction $guirolloveraction) "showservers" [
        guitooltiptext "displays the server browser for other online matches"
    ] "showgui profile" [
        guitooltiptext "edit your name, colour, and model type"
    ] "showgui editing" [
        guitooltiptext "access commands to use while editing a map"
    ] "showgui irc" [
        guitooltiptext "shows your open irc windows"
    ]  "disconnect" [
        guitooltiptext "disconnects your current connection"
    ] "showgui maps 2" [
        guitooltiptext "displays the current votes"
    ] "showgui maps 1" [
        guitooltiptext (format "select a map/mode combination to %1" (? (isonline) "vote for" "practice on"))
    ] "showgui options" [
        guitooltiptext "edit your configuration options"
    ] "showgui vars" [
        guitooltiptext "edit gameplay and weapon variables"
    ] "showgui help" [
        guitooltiptext "gets some help!"
    ] "quit" [
        guitooltiptext "closes Red Eclipse"
    ] () [
        guitooltiptext ""
    ]
    guitip
]

guimodes = [
    guicenter [ guitext (modedesc @arg1 @arg2 5) ]
    if (> $arg2 0) [
        loop i $mutsidxnum [
            mut = (<< 1 $i)
            if (&& [& $arg2 $mut] [! (& (mutsimplied $arg1) $mut)]) [
                muttxt = (mutsdesc $arg1 $mut 4)
                if (strlen $muttxt) [ guicenter [ guitext (concatword "^fa" $muttxt) ] ]
            ]
        ]
    ]
]

newgui loading [
    nonworld [ guistayopen [ guinohitfx [
        guione [
            guifont "huge" [ guicenter [ guitext (? (&& (isloadingmap) (strlen $maptitle)) $maptitle "Loading") ] ]
            guifont "default" [ guicenter [ guitext (? (&& (isloadingmap) (strlen $maptitle) (strlen $mapauthor)) (concat "by" $mapauthor) (? (strlen $progresstitle) $progresstitle "please wait..")) ] ]
            guifont "reduced" [
                if (&& (isloadingmap) (isconnected)) [
                    if (= (isloadingmap) 2) [
                        guicenter [ guitext "^fs^fylightmapping^fS" ]
                    ] [
                        gname = (gamename (gamemode) (mutators) 0 32)
                        guicenter [ guitext (format "^fs^fy%1^fS" $gname) ]
                    ]
                ]
                if (strlen $connectname) [
                    guicenter [ guitext (format "^faon: ^fw%1:[%2]" $connectname $connectport) ]
                    if (&& (isconnected) (strlen $serverdesc)) [ guifont "little" [ guicenter [ guitext (format "^"^fs%1^fS^"" $serverdesc) ] ] ]
                ]
            ]
            case (isloadingmap) 2 [
                guistrut 0.5
                guicenter [ guiimage "" [] 7 0 ]
            ] 1 [
                if (isconnected) [
                    guistrut 0.5
                    guifont "little" [
                        guicenter [
                            modetexs = (modetexlist (gamemode) (mutators))
                            looplist modetex $modetexs [ guiimage $modetex [] 1 0 "textures/blank" ]
                        ]
                        guimodes (gamemode) (mutators)
                    ]
                ]
            ]
        ]
        guitip
    ] ] ]
]

newgui about [
    guione [
        guicenter [ guitext "Red Eclipse Engine" ]
        guifont "little" [
            guicenter [ guitext "(C) 2010-2013 Quinton Reeves, Lee Salzman" ]
            guicenter [ guitext "http://www.redeclipse.net/" ]
        ]
        guistrut 1
        guicenter [ guitext "Red Eclipse Data" ]
        guifont "little" [
            guicenter [ guitext "(C) 2010-2013 Red Eclipse Team" ]
            guicenter [ guitext "http://www.redeclipse.net/" ]
        ]
        guistrut 1
        guicenter [ guitext "Based on Cube Engine 2" ]
        guifont "little" [
            guicenter [ guitext "(C) 2001-2013 Wouter van Oortmerssen, Lee Salzman" ]
            guicenter [ guitext "Mike Dysart, Robert Pointon, and Quinton Reeves" ]
            guicenter [ guitext "http://sauerbraten.org/" ]
        ]
    ]
]

newgui support [
    ircgui freenode
] [
    if (= $guipasses 0) [
        ircaddclient freenode irc.freenode.net 6667 (getplayername)
        ircaddchan freenode #redeclipse
    ]
]

newgui help [
    guione [
        guifont "emphasis" [
            guicenter [ guibutton "about" "showgui about" ]
            guicenter [ guibutton "live support" "showgui support" ]
        ]
    ]
]
