bind ESCAPE [if (cleargui 1) [] [showgui main]]    // it all starts here

newgui main [
    guitip
    guistrut 0.5
    guibox [guiimage "textures/emblem" "showgui servers" 7.5 1 "textures/nothumb"] [
        guifont "default" [
            guicenter [ guibutton "profile" "showgui profile" ]
            guistrut 0.5
            if (isconnected) [
                if (= (gamemode) $modeidxediting) [
                    guicenter [ guibutton "editing" "showgui editing" ]
                    guistrut 0.5
                ]
                guicenter [ guibutton "disconnect" "disconnect" ]
                if (> (getvote) 0) [ guicenter [ guibutton "show votes" "showgui maps 2" ] ]
                guistrut 0.5
            ]
            guicenter [ guibutton (? (isonline) "find servers" "play online") "showgui servers" ]
            guicenter [ guibutton (? (isconnected) "vote map/mode" "offline practice") "showgui maps 1" ]
            guistrut 0.5
            if (> (ircconns) 0) [ guicenter [ guibutton "irc windows" "showgui irc" ] ]
            guicenter [ guibutton "options" "showgui options" ]
            guicenter [ guibutton "variables" "showgui vars" ]
            guicenter [ guibutton "help" "showgui help" ]
            guistrut 0.5
            guicenter [ guibutton "quit" "quit" ]
        ]
    ]
    guistrut 0.5
    guilist [
        guistrut 0.5
        guilist [
            guifont "little" [
                cases (? (strlen $guirolloverimgaction) $guirolloverimgaction $guirolloveraction) "showgui servers" [
                    guitext "displays the server browser for other online matches"
                ] "showgui profile" [
                    guitext "edit your name, colour, and model type"
                ] "showgui editing" [
                    guitext "access commands to use while editing a map"
                ] "showgui irc" [
                    guitext "shows your open irc windows"
                ]  "disconnect" [
                    guitext "disconnects your current connection"
                ] "showgui maps 2" [
                    guitext "displays the current votes"
                ] "showgui maps 1" [
                    guitext (format "select a map/mode combination to %1" (? (isonline) "vote for" "practice on"))
                ] "showgui options" [
                    guitext "edit your configuration options"
                ] "showgui vars" [
                    guitext "edit gameplay and weapon variables"
                ] "showgui help" [
                    guitext "gets some help!"
                ] "quit" [
                    guitext "closes Red Eclipse"
                ] () [
                    guitext "click on an activity to start"
                ]
            ]
        ]
    ]
]

guimodes = [
    guicenter [ guitext (modedesc @arg1 @arg2 5) ]
    if (> $arg2 0) [
        loop i $mutsidxnum [
            mut = (<< 1 $i)
            if (& $arg2 $mut) [
                muttxt = (mutsdesc $arg1 $mut 4)
                if (strlen $muttxt) [ guicenter [ guitext (concatword "^fa" $muttxt) ] ]
            ]
        ]
    ]
]

newgui loading [
    nonworld [ guistayopen [ guinohitfx [
        guitip
        guistrut 0.5
        guilist [
            guiimage "" [] 7.5 1
            guistrut 1
            guicenter [
                guistrut 50 1
                guifont "emphasis" [ guicenter [ guitext (? (strlen $maptitle) $maptitle "Loading..") ] ]
                guifont "default" [
                    guicenter [ guitext (? (strlen $maptitle) (concat "^faby" (? (strlen $mapauthor) $mapauthor "Unknown")) "Please wait..") ]
                ]
                guifont "little" [
                    if (strlen $connectname) [ guicenter [ guitext (format "^faon: ^fw%1:[%2]" $connectname $connectport) ] ]
                    if (isconnected) [
                        gname = (gamename (gamemode) (mutators) 0 32)
                        guicenter [ guitext (format "playing: [ ^fs^fa%1^fS ]" $gname) ]
                        guicenter [
                            modetexs = (modetexlist (gamemode) (mutators))
                            looplist modetex $modetexs [ guiimage $modetex [] 1 0 "textures/blank" ]
                        ]
                        guimodes (gamemode) (mutators)
                    ]
                ]
            ]
        ]
    ] ] ]
]

newgui about [
    guibox [guiimage "textures/emblem" "cleargui 1" 7.5 1 "textures/nothumb"] [
        guicenter [ guitext "Red Eclipse Engine" ]
        guifont "little" [
            guicenter [ guitext "(C) 2010-2013 Quinton Reeves, Lee Salzman" ]
            guicenter [ guitext "http://www.redeclipse.net/" ]
        ]
        guistrut 1
        guicenter [ guitext "Red Eclipse Data" ]
        guifont "little" [
            guicenter [ guitext "(C) 2010-2013 Red Eclipse Team" ]
            guicenter [ guitext "http://www.redeclipse.net/" ]
        ]
        guistrut 1
        guicenter [ guitext "Based on Cube Engine 2" ]
        guifont "little" [
            guicenter [ guitext "(C) 2001-2013 Wouter van Oortmerssen, Lee Salzman" ]
            guicenter [ guitext "Mike Dysart, Robert Pointon, and Quinton Reeves" ]
            guicenter [ guitext "http://sauerbraten.org/" ]
        ]
    ]
]

newgui support [
    ircgui freenode
] [
    if (= $guipasses 0) [
        ircaddclient freenode irc.freenode.net 6667 (getplayername)
        ircaddchan freenode #redeclipse
    ]
]

newgui help [
    guibox [guiimage "textures/emblem" "cleargui 1" 7.5 1 "textures/nothumb"] [
        guifont "default" [
            guicenter [ guibutton "about" "showgui about" ]
            guicenter [ guibutton "live support" "showgui support" ]
        ]
    ]
]
