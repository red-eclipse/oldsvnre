okprofile = [setinfo $playerprevname $playerprevcolour $playerprevmodel $playerprevvanity; cleargui 1]

playerprevinherit = 0
playerprevdisinherit = 0

newgui playerprev [
    guiheader "player preview"
    guicenter [ guiplayerpreview $playerprevmodel $playerprevcolour $playerprevteam $playerprevweap $playerprevvanity [cleargui 1] 15 1 1 ]
    guistrut 0.25
    guicenter [
        guilistx 2 [
            guitext "team: "
            teamneutraltex = $teamtex
            loop t 5 [
                push t (at "neutral alpha omega kappa sigma" $t) [
                    guistayopen [ guifont "default" [ guibutton (format "^f[%1]^f(%2)" $[team@[t]colour] $[team@[t]tex]) [playerprevteam = @@@@t] ] ]
                ]
            ]
            guistrut 1
            guitext "weapon: "
            loop w $weapidxnum [
                push w (at $weapname $w) [
                    guistayopen [ guifont "emphasis" [ guibutton (format "^f[%1]^f(%2)" $[@[w]colour] [textures/weapons/@w]) [playerprevweap = @@@@w] ] ]
                ]
            ]
        ]
    ]
] [
    if (= $guipasses 0) [
        if $playerprevinherit [ playerprevinherit = 0; playerprevdisinherit = 1 ] [
            playerprevteam = (getplayerteam 1)
            playerprevweap = (weapselect)
            playerprevname = (getplayername)
            playerprevcolour = (getplayercolour -1)
            playerprevmodel = (getplayermodel)
            playerprevvanity = (getplayervanity)
        ]
    ]
]

newgui profile [
    guibox [guiplayerpreview $playerprevmodel $playerprevcolour $playerprevteam $playerprevweap $playerprevvanity [playerprevinherit = 1; showgui playerprev] 7.5 1 1] [
        guicenter [ guifont "emphasis" [ guitext "player name" ] ]
        guicenter [ guifield playerprevname 24 [] ]
        guispring 1
        guicenter [ guifont "emphasis" [ guitext "colour tone" ] ]
        guicenter [
            colr = (& (>> $playerprevcolour 16) 0xFF)
            colg = (& (>> $playerprevcolour 8) 0xFF)
            colb = (& $playerprevcolour 0xFF)
            guilist [
                guibackground $playerprevcolour
                guistrut 6 1
                guistrut 3
            ]
            guistrut 1
            guilist [
                guistrut 30 1
                loop i 3 [
                    n = [col@(at "r g b" $i)]
                    guislider $n 0 255 [
                        playerprevcolour = (+ (<< $colr 0x10) (<< $colg 0x8) $colb)
                    ] 0 1 (at [0xFF0000 0x00FF00 0x0000FF] $i) 1 (<< $$n (* (- 2 $i) 8))
                ]
            ]
        ]
        guispring 1
        guicenter [ guifont "emphasis" [ guitext "gender" ] ]
        guicenter [
            guiradio "male" playerprevmodel 0
            guistrut 1
            guiradio "female" playerprevmodel 1
        ]
    ] [
        guicenter [ guifont "super" [
            guibutton "^fgok" [okprofile] []
            guispring 1
            if (needname) [guitext "^fdcancel"] [guibutton "^focancel" [cleargui 1]]
        ] ]
    ]
    guitab "vanity items"
    guibox [guiplayerpreview $playerprevmodel $playerprevcolour $playerprevteam $playerprevweap $playerprevvanity [playerprevinherit = 1; showgui playerprev] 7.5 1 1] [
        guicenter [
            guilist [
                vanityset = (listlen $playerprevvanity)
                loop z $vanityset [
                    vaint = (at $playerprevvanity $z)
                    vainn = (findvanity $vaint)
                    if (>= $vainn 0) [
                        guistayopen [ guicenter [ guibutton (format "^fy%1" (getvanity $vainn 2)) [ playerprevvanity = (listdel $playerprevvanity @(escape $vaint)) ] ] ]
                    ]
                ]
            ]
            guistrut 1
            guispring 1
            guistrut 1
            guilist [
                guifont "little" [
                    vanitynum = (getvanity)
                    guiloopsplit z 3 $vanitynum [
                        vaint = (getvanity $z 1)
                        vainn = (indexof $playerprevvanity $vaint)
                        if (< $vainn 0) [
                            guistayopen [ guicenter [ guibutton (format "^fa%1" (getvanity $z 2)) [ playerprevvanity = (concat $playerprevvanity @(escape $vaint)) ] ] ]
                        ]
                    ] [ guistrut 2 ]
                ]
            ]
        ]
    ] [
        guicenter [ guifont "super" [
            guibutton "^fgok" [okprofile] []
            guispring 1
            if (needname) [guitext "^fdcancel"] [guibutton "^focancel" [cleargui 1]]
        ] ]
    ]
    guitab "view options"
    guibox [guiplayerpreview $playerprevmodel $playerprevcolour $playerprevteam $playerprevweap $playerprevvanity [playerprevinherit = 1; showgui playerprev] 7.5 1 1] [
        guicenter [
            guicheckbox "allow vanity models" vanitymodels
            guistrut 1
            guicheckbox "allow headless models" headlessmodels
        ]
        guicenter [
            guicheckbox "player preview in main menu" showmainplayerprev
        ]
        guistrut 0.5
        guicenter [ guitext "player colour tones" ]
        guistrut 0.5
        guicenter [
            guilistsplit n 2 "0 1" [ guispring 1; guilistx 2 [ guitext (format "%1: " (at "primary secondary" $n)) ]; guistrut 1 ]
            guilistsplit n 2 "0 1" [
                guilist [
                    guistrut 30 1
                    guislider player@(at "overtone undertone" $n) -1 6 [] 0 1
                    guifont "consub" [
                        case $player@(at "overtone undertone" $n) -1 [
                            guitext "^faprofile colour overrides all, no tones applied"
                        ] 0 [
                            guitext "^fateam colours only, profile not used"
                        ] 1 [
                            guitext "^faprofile colour regardless of team"
                        ] 2 [
                            guitext "^faprofile colour when neutral, team when teamed"
                        ] 3 [
                            guitext "^faneutral colour when neutral, profile when teamed"
                        ] 4 [
                            guitext "^faprofile colour mixed with team"
                        ] 5 [
                            guitext "^faprofile colour mixed with team (but neutral)"
                        ] 6 [
                            guitext "^faprofile colour overrides team, only neutral mixed"
                        ]
                    ]
                ]
            ]
        ]
        guistrut 1
        guicenter [
            guicenter [
                guicenter [ guitext "preview options" ]
                guistrut 0.5
                guicenter [
                    teamneutraltex = $teamtex
                    loop t 5 [
                        push t (at "neutral alpha omega kappa sigma" $t) [
                            guistayopen [ guifont "default" [ guibutton (format "^f[%1]^f(%2)" $[team@[t]colour] $[team@[t]tex]) [playerprevteam = @@@@t] ] ]
                        ]
                    ]
                ]
                guistrut 0.25
                guicenter [
                    loop w $weapidxnum [
                        push w (at $weapname $w) [
                            guistayopen [ guifont "emphasis" [ guibutton (format "^f[%1]^f(%2)" $[@[w]colour] [textures/weapons/@w]) [playerprevweap = @@@@w] ] ]
                        ]
                    ]
                ]
            ]
        ]
    ] [
        guicenter [ guifont "super" [
            guibutton "^fgok" [okprofile] []
            guispring 1
            if (needname) [guitext "^fdcancel"] [guibutton "^focancel" [cleargui 1]]
        ] ]
    ]
    guitab "user account"
    guibox [guiplayerpreview $playerprevmodel $playerprevcolour $playerprevteam $playerprevweap $playerprevvanity [playerprevinherit = 1; showgui playerprev] 7.5 1 1] [
        guicenter [ guitext "username " ]
        guicenter [ guifield accountname 48 [] ]
        guistrut 0.5
        guicenter [ guitext "private key " ]
        guicenter [ guifield accountpass 48 [] ]
        guistrut 1
        guicenter [ guicheckbox "identify on connect" authconnect ]
        guicenter [ guicheckbox "identify when playing offline" quickauthchecks ]
        guistrut 1
        guicenter [ guitext "^fyaccount applications are available at" ]
        guicenter [ guitext "^fchttp://redeclipse.net/apply" ]
        guistrut 1
    ] [
        guicenter [ guifont "super" [
            guibutton "^fgok" [okprofile] []
            guispring 1
            if (needname) [guitext "^fdcancel"] [guibutton "^focancel" [cleargui 1]]
        ] ]
    ]
] [
    if (= $guipasses 0) [
        if $playerprevdisinherit [ playerprevdisinherit = 0 ] [
            playerprevteam = (getplayerteam 1)
            playerprevweap = (weapselect)
            playerprevname = (getplayername)
            playerprevcolour = (getplayercolour -1)
            playerprevmodel = (getplayermodel)
            playerprevvanity = (getplayervanity)
        ]
    ]
]