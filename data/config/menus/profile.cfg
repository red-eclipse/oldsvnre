newgui profile [
    guibox [guiplayerpreview $modlval $colrval $teamval $weapval $vntyval [cleargui 1] 7.5 1 (? (strcmp $versionuname "mekarcade") 0.75 1)] [
        guilist [
            guicenter [
                guicenter [
                    guitext "name:  "
                    guifield nameval 24 []
                ]
                guistrut 0.5
                guicenter [
                    if (strcmp $versionuname "mekarcade") [
                        guitext "mek:"
                        guistrut 4
                        guicenter [
                            guilist [ guistrut 20 ]
                            guislider modlval 0 3
                        ]
                    ] [
                        guitext "model: "
                        guiradio "male" modlval 0
                        guistrut 1
                        guiradio "female" modlval 1
                    ]
                ]
                guistrut 1
                guicenter [
                    colr = (& (>> $colrval 16) 0xFF)
                    colg = (& (>> $colrval 8) 0xFF)
                    colb = (& $colrval 0xFF)
                    guilist [
                        guibackground $colrval
                        guistrut 6 1
                        guistrut 3
                    ]
                    guistrut 1
                    guilistsplit n 3 "0 1 2" [
                        guilist [
                            guistrut 30 1
                            guislider col@(at "r g b" $n) 0 255 [
                                colrval = (| (| (<< $colr 16) (<< $colg 8)) $colb)
                            ] 0 1 0x@(at "FF0000 00FF00 0000FF" $n)
                        ]
                    ]
                ]
            ]
            if (! (strcmp $versionuname "mekarcade")) [
                guistrut 2
                guicenter [
                    vanitynum = (getvanity)
                    loop z $vanitynum [
                        vaint = (getvanity $z 1)
                        found = (>= (indexof $vntyval $vaint) 0)
                        guistayopen [ guibutton (getvanity $z 2) [
                            vntyval = ( @(? $found listdel concat) $vntyval @(escape $vaint) )
                        ] [] (? $found "checkboxon" "checkbox") ]
                    ]
                ]
            ]
        ]
        guistrut 1
        guicenter [ guifont "super" [
            guibutton "^fgok" [setinfo $nameval $colrval $modlval $vntyval; cleargui 1] [] "action"
            guispring 1
            guibutton "^focancel" [cleargui 1] [] "exit"
        ] ]
        guistrut 1
        guitext $vntyval
    ]
    guitab "account"
    guibox [guiplayerpreview $modlval $colrval $teamval $weapval $vntyval [cleargui 1] 7.5 1 (? (strcmp $versionuname "mekarcade") 0.75 1)] [
        guifont "emphasis" [ guicenter [ guitext "Red Eclipse Account" ] ]
        guistrut 1
        guicenter [
            guitext "user: "
            guifield accountname 48 []
        ]
        guicenter [
            guitext "pass: "
            guifield accountpass 48 []
        ]
        guistrut 1
        guicenter [
            guicheckbox "identify on connect" authconnect
        ]
    ]
    guitab "view options"
    guibox [guiplayerpreview $modlval $colrval $teamval $weapval $vntyval [cleargui 1] 7.5 1 (? (strcmp $versionuname "mekarcade") 0.75 1)] [
        guicenter [ guifont "reduced" [ guitext "player colour tones" ] ]
        guistrut 0.5
        guicenter [
            guilistsplit n 2 "0 1" [ guispring 1; guilist [ guilist [ guitext (format "%1: " (at "primary secondary" $n)) ]; guistrut 1 ] ]
            guilistsplit n 2 "0 1" [
                guilist [
                    guistrut 30 1
                    guislider player@(at "overtone undertone" $n) -1 6 [] 0 1
                    guifont "consub" [
                        case $player@(at "overtone undertone" $n) -1 [
                            guitext "^faprofile colour overrides all, no tones applied"
                        ] 0 [
                            guitext "^fateam colours only, profile not used"
                        ] 1 [
                            guitext "^faprofile colour regardless of team"
                        ] 2 [
                            guitext "^faprofile colour when neutral, team when teamed"
                        ] 3 [
                            guitext "^faneutral colour when neutral, profile when teamed"
                        ] 4 [
                            guitext "^faprofile colour mixed with team"
                        ] 5 [
                            guitext "^faprofile colour mixed with team (but neutral)"
                        ] 6 [
                            guitext "^faprofile colour overrides team, only neutral is mixed"
                        ]
                    ]
                ]
            ]
        ]
        guistrut 1
        guicenter [
            guicenter [
                guicenter [ guifont "reduced" [ guitext "preview options" ] ]
                guistrut 0.5
                guicenter [
                    teamneutraltex = $teamtex
                    loop t 5 [
                        push t (at "neutral alpha omega kappa sigma" $t) [
                            guistayopen [ guifont "default" [ guibutton (format "^f[%1]^f(%2)" $[team@[t]colour] $[team@[t]tex]) [teamval = @@@@t] ] ]
                        ]
                    ]
                ]
                guistrut 0.25
                guicenter [
                    loop w $weapidxnum [
                        push w (at $weapname $w) [
                            guistayopen [ guifont "emphasis" [ guibutton (format "^f[%1]^f(%2)" $[@[w]colour] [textures/weapons/@w]) [weapval = @@@@w] ] ]
                        ]
                    ]
                ]
            ]
        ]
    ]
] [
    if (= $guipasses 0) [
        teamval = 0
        weapval = 0
        nameval = (getplayername)
        colrval = (getplayercolour -1)
        modlval = (getplayermodel)
        vntyval = (getplayervanity)
    ]
]
