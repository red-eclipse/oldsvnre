okprofile = [setinfo $nameval $colrval $modlval $vntyval; cleargui 1]

newgui profile [
    guibox [guiplayerpreview $modlval $colrval $teamval $weapval $vntyval [okprofile] 7.5 1 1] [
        guicenter [ guifont "emphasis" [ guitext "player name" ] ]
        guicenter [ guifield nameval 24 [] ]
        guispring 1
        guicenter [ guifont "emphasis" [ guitext "colour tone" ] ]
        guicenter [
            colr = (& (>> $colrval 16) 0xFF)
            colg = (& (>> $colrval 8) 0xFF)
            colb = (& $colrval 0xFF)
            guilist [
                guibackground $colrval
                guistrut 6 1
                guistrut 3
            ]
            guistrut 1
            guilistsplit n 3 "0 1 2" [
                guilist [
                    guistrut 30 1
                    colh = (case $n 0 [0xFF0000] 1 [0x00FF00] 2 [0x0000FF])
                    guislider col@(at "r g b" $n) 0 255 [
                        colrval = (| (| (<< $colr 16) (<< $colg 8)) $colb)
                    ] 0 1 $colh 1 (| (| (<< (? (= $n 0) $colr 0) 16) (<< (? (= $n 1) $colg 0) 8)) (? (= $n 2) $colb 0))
                ]
            ]
        ]
        guispring 1
        guicenter [ guifont "emphasis" [ guitext "gender" ] ]
        guicenter [
            guiradio "male" modlval 0
            guistrut 1
            guiradio "female" modlval 1
        ]
    ] [
        guicenter [ guifont "super" [
            guibutton "^fgok" [okprofile] []
            guispring 1
            if (needname) [guitext "^fdcancel"] [guibutton "^focancel" [cleargui 1]]
        ] ]
    ]
    guitab "vanity items"
    guibox [guiplayerpreview $modlval $colrval $teamval $weapval $vntyval [okprofile] 7.5 1 1] [
        guicenter [
            guilist [
                vanityset = (listlen $vntyval)
                loop z $vanityset [
                    vaint = (at $vntyval $z)
                    vainn = (findvanity $vaint)
                    if (>= $vainn 0) [
                        guistayopen [ guicenter [ guibutton (format "^fy%1" (getvanity $vainn 2)) [ vntyval = (listdel $vntyval @(escape $vaint)) ] ] ]
                    ]
                ]
            ]
            guistrut 1
            guispring 1
            guistrut 1
            guilist [
                guifont "little" [
                    vanitynum = (getvanity)
                    guiloopsplit z 3 $vanitynum [
                        vaint = (getvanity $z 1)
                        vainn = (indexof $vntyval $vaint)
                        if (< $vainn 0) [
                            guistayopen [ guicenter [ guibutton (format "^fa%1" (getvanity $z 2)) [ vntyval = (concat $vntyval @(escape $vaint)) ] ] ]
                        ]
                    ] [ guistrut 2 ]
                ]
            ]
        ]
    ] [
        guicenter [ guifont "super" [
            guibutton "^fgok" [okprofile] []
            guispring 1
            if (needname) [guitext "^fdcancel"] [guibutton "^focancel" [cleargui 1]]
        ] ]
    ]
    guitab "view options"
    guibox [guiplayerpreview $modlval $colrval $teamval $weapval $vntyval [okprofile] 7.5 1 1] [
        guicenter [
            guicheckbox "allow vanity models" vanitymodels
            guistrut 1
            guicheckbox "allow headless models" headlessmodels
        ]
        guistrut 0.5
        guicenter [ guitext "player colour tones" ]
        guistrut 0.5
        guicenter [
            guilistsplit n 2 "0 1" [ guispring 1; guilist [ guilist [ guitext (format "%1: " (at "primary secondary" $n)) ]; guistrut 1 ] ]
            guilistsplit n 2 "0 1" [
                guilist [
                    guistrut 30 1
                    guislider player@(at "overtone undertone" $n) -1 6 [] 0 1
                    guifont "consub" [
                        case $player@(at "overtone undertone" $n) -1 [
                            guitext "^faprofile colour overrides all, no tones applied"
                        ] 0 [
                            guitext "^fateam colours only, profile not used"
                        ] 1 [
                            guitext "^faprofile colour regardless of team"
                        ] 2 [
                            guitext "^faprofile colour when neutral, team when teamed"
                        ] 3 [
                            guitext "^faneutral colour when neutral, profile when teamed"
                        ] 4 [
                            guitext "^faprofile colour mixed with team"
                        ] 5 [
                            guitext "^faprofile colour mixed with team (but neutral)"
                        ] 6 [
                            guitext "^faprofile colour overrides team, only neutral is mixed"
                        ]
                    ]
                ]
            ]
        ]
        guistrut 1
        guicenter [
            guicenter [
                guicenter [ guitext "preview options" ]
                guistrut 0.5
                guicenter [
                    teamneutraltex = $teamtex
                    loop t 5 [
                        push t (at "neutral alpha omega kappa sigma" $t) [
                            guistayopen [ guifont "default" [ guibutton (format "^f[%1]^f(%2)" $[team@[t]colour] $[team@[t]tex]) [teamval = @@@@t] ] ]
                        ]
                    ]
                ]
                guistrut 0.25
                guicenter [
                    loop w $weapidxnum [
                        push w (at $weapname $w) [
                            guistayopen [ guifont "emphasis" [ guibutton (format "^f[%1]^f(%2)" $[@[w]colour] [textures/weapons/@w]) [weapval = @@@@w] ] ]
                        ]
                    ]
                ]
            ]
        ]
    ] [
        guicenter [ guifont "super" [
            guibutton "^fgok" [okprofile] []
            guispring 1
            if (needname) [guitext "^fdcancel"] [guibutton "^focancel" [cleargui 1]]
        ] ]
    ]
    guitab "user account"
    guibox [guiplayerpreview $modlval $colrval $teamval $weapval $vntyval [okprofile] 7.5 1 1] [
        guicenter [ guitext "username " ]
        guicenter [ guifield accountname 48 [] ]
        guistrut 0.5
        guicenter [ guitext "private key " ] 
        guicenter [ guifield accountpass 48 [] ]
        guistrut 1
        guicenter [
            guicheckbox "identify on connect" authconnect
            //guispring 1
            //guicheckbox "identify when playing offline" quickauthchecks
        ]
        guistrut 1
        guicenter [ guitext "^fyaccount applications are available at" ]
        guicenter [ guitext "^fchttp://redeclipse.net/apply" ]
        guistrut 1
    ] [
        guicenter [ guifont "super" [
            guibutton "^fgok" [okprofile] []
            guispring 1
            if (needname) [guitext "^fdcancel"] [guibutton "^focancel" [cleargui 1]]
        ] ]
    ]
] [
    if (= $guipasses 0) [
        teamval = (getplayerteam 1)
        weapval = (weapselect)
        nameval = (getplayername)
        colrval = (getplayercolour -1)
        modlval = (getplayermodel)
        vntyval = (getplayervanity)
    ]
]
