// compass/voice actions

voices = 0
addvoice = [
    voices = (+ $voices 1)
    alias (format "voice%1str" $voices) $arg1
    alias (format "voice%1num" $voices) $arg2
    loop i $arg2 [
        sndname = $arg3
        if (> $i 0) [ sndname = (format "%1%2" $arg3 (+ $i 1)) ]
        alias (format "voice%1sn%2" $voices $i) (registersound $sndname 255 512 8 1)
    ]
]

addvoice "argh"         2 "voice/argh"
addvoice "lucky shot"   2 "voice/luckyshot"
addvoice "nice shot"    2 "voice/niceshot"
addvoice "boom"         2 "voice/boom"
addvoice "damn"         2 "voice/damnit"
addvoice "haha"         2 "voice/haha"
addvoice "suck"         2 "voice/suckit"
addvoice "pzap"         2 "voice/pzap"
addvoice "yes~"         2 "voice/yes"
addvoice "sorry"        2 "voice/sorry"
addvoice "no problem"   2 "voice/noproblem"
addvoice "np~"          2 "voice/noproblem"
addvoice "defend"       2 "voice/defend"
addvoice "no~"          2 "voice/no"
addvoice "go go go"     2 "voice/gogogo"
addvoice "gogogo"       2 "voice/gogogo"
addvoice "hang on"      2 "voice/hangon"
addvoice "thanks"       2 "voice/thanks"

on_text = [
    id = 0
    reg = (format "%1~" $arg4) // the ~ allows absolute matching, like in gg
    loopwhile i $voices [= $id 0] [
        str = (getalias (format "voice%1str" (+ $i 1)))
        if (strncasecmp $str $reg (strlen $str)) [ id = (+ $i 1) ]
    ]
    result (getalias (format "voice%1sn%2" $id (rnd (getalias (format "voice%1num" $id)))))
]
